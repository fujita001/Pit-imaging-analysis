//This macro extracts image sequences of ROIs and saves them in a folder after opening image sequences by specifying a folder and selecting multiple ROIs. The detection sensitivity changes with the value of setThshold.

//Select a folder with time-lapse RAW images
showMessage("After opening the image sequence, select the ROI as rectangle and run this macro to extract and save the image sequence of the ROI."); 
openDir = getDirectory("Choose a Directory"); 
list = getFileList(openDir);//Store all file names in list

//ROI Image directory created in the same hierarchy as the selected folder.
slashsplit =split(openDir, "/");// Split into slashes and store in slashesplit array
Array.print(slashsplit);
print(slashsplit.length);
print(slashsplit[slashsplit.length-1]);
roiDir=replace(openDir, slashsplit[slashsplit.length-1],"ROI_" + slashsplit[slashsplit.length-1]);
print(roiDir);
File.makeDirectory(roiDir);
print("ROI directory =", roiDir);

//Make Pit Area Results directory
ResultsDir = roiDir + "PitAreaResults/";
File.makeDirectory(ResultsDir);
print("Pit Area Results directory =", ResultsDir);

//Select ROI
run("Image Sequence...", "dir=&openDir sort"); //Cannot be opened without &.
print("test");

run("ROI Manager...");//Storing ROI location information
waitForUser("Set ROI in ROI manager with Add and click OK (multiple ROIs can be set)."); 
NumRoi = roiManager("count");	

//Loop around and process all images sequentially
for (j=0; j<NumRoi; j++){
//Create folders for each ROI
File.makeDirectory(roiDir + "/ROI" + j);
	for (i=0; i<list.length; i++){
		//Open an image of the specified location
		open(openDir+list[i]);
		name = getTitle();
		
		//Apply the stored ROI to the opened image to create a clop image and save it.
		roiManager("Select", j);
		run("Copy");
		run("Internal Clipboard");
		saveAs("Png", roiDir + "/ROI" + j + "/" + name);
		
		//Pit area calculation process
		run("8-bit");
		setAutoThreshold("Default");
		setThreshold(0, 20);
		run("Measure");

		close();
		close();
	}
	//Resultsの保存
	saveAs("Results", ResultsDir + name + "ROI" + j + ".csv");
	close("Results");

}
run("Close");
roiManager("Delete");
