//Choose a Directory of Directorys
showMessage("Select Open Folder"); 
openDirDir = getDirectory("Choose a Directory of Directorys"); 
dirlist = getFileList(openDirDir);

print("Directory for Directories =", openDirDir);

//open folder name list
Array.show(dirlist); 

//Make drift corrected directory
correctDirDir = openDirDir + "DriftCorrected/";
File.makeDirectory(correctDirDir);
print("corrected directory of directories =", correctDirDir);

//Loop around and process all images sequentially
for (i=0; i<dirlist.length; i++){
	//open a folder in the specified location
	openDir = openDirDir+dirlist[i];
	list = getFileList(openDir);

	//Make movies
	File.openSequence(openDir);
	run("Reverse"); //Re-order in reverse order to make the first one with a large drift so that the back one, which is not moving, is the standard.

	//get folder & file name
	seqName = getTitle();
	print("folder & file name =", seqName);

	//Make corrected directory
	correctDir = correctDirDir + seqName + "/";
	File.makeDirectory(correctDir);
	print("corrected directory =", correctDir);

	//Get file name
	correctnjt = correctDirDir + seqName + ".njt";
	print("correctnjt path=", correctnjt);
	run("F4DR Estimate Drift", "time=1 max=50 reference=[previous frame (better for live)] apply choose=correctnjt");

	//Save TIFF image 	
	run("Reverse");//Drift correction completed and rearranged in original order
	run("Image Sequence... ", "select=&correctDir dir=&correctDir format=TIFF use");
	
	close();
	close();
}
